{
  parserClass="se.clau.ironclad.language.ErlangParser"
  parserUtilClass="se.clau.ironclad.language.ErlangParserUtil"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  tokens = [
    PLUS            = "+"
    MINUS           = "-"
    MULTIPLY        = "*"
    FLOAT_DIV       = "/"
    EQEQ            = "=="
    NEQ             = "/="
    STRONG_EQ       = "=:="
    STRONG_NEQ      = "=/="
    GT              = ">"
    GTEQ            = ">="
    LT              = "<"
    LTEQ            = "=<"
    PLUS_PLUS       = "++"
    MINUS_MINUS     = "--"
    EQ              = "="
    BANG            = "!"
    MAYBE_EQ        = "?="

    L_DOUBLE_ANGLE  = "<<"
    R_DOUBLE_ANGLE  = ">>"

    L_PAREN         = "("
    R_PAREN         = ")"
    L_CURLY         = "{"
    R_CURLY         = "}"
    L_SQUARE        = "["
    R_SQUARE        = "]"
    COLON           = ":"
    COLON_COLON     = "::"
    DOUBLE_BAR      = "||"
    VERTICAL_BAR    = "|"
    SEMICOLON       = ";"
    COMMA           = ","
    PERIOD          = "."
    HASH_SYMBOL     = "#"
    QUESTION        = "?"
    MATCH           = ":="
    SINGLE_QUOTE    = "'"

    R_DOUBLE_ARROW  = "=>"
    L_DOUBLE_ARROW  = "<="
    L_ARROW         = "<-"
    R_ARROW         = "->"

    INTEGER_DIV     = "div"
    REMAINDER       = "rem"
    OR              = "or"
    XOR             = "xor"
    AND             = "and"
    NOT             = "not"
    BINARY_OR       = "bor"
    BINARY_XOR      = "bxor"
    BINARY_AND      = "band"
    BINARY_NOT      = "bnot"
    BINARY_SHIFT_L  = "bsl"
    BINARY_SHIFT_R  = "bsr"
    ANDALSO         = "andalso"
    ORELSE          = "orelse"

    // These tokens must never appear in final PSI, and are eliminated by ErlangFlexAdapter while lexing
    DOLLAR_CHAR     = "$-prefixed character"
    PP_PASTE        = "preprocessor pasted macro"
    PP_DEFINE       = "preprocessor -define"
    PP_UNDEF        = "preprocessor -undef"
    PP_IFDEF        = "preprocessor -ifdef"
    PP_IF           = "preprocessor -if"
    PP_ELIF         = "preprocessor -elif"
    PP_IFNDEF       = "preprocessor -ifndef"
    PP_ELSE         = "preprocessor -else"
    PP_ENDIF        = "preprocessor -endif"
    PP_INCLUDE      = "preprocessor -include"
    PP_INCLUDELIB   = "preprocessor -include_lib"
    INTEGER_LITERAL = "integer literal"
    STRING_LITERAL  = "integer literal"
  ]

  psiClassPrefix="ErlPsi"
  psiImplClassSuffix="Impl"
  psiPackage="se.clau.ironclad.language.psi"
  psiImplPackage="se.clau.ironclad.language.psi.impl"

  elementTypeHolderClass="se.clau.ironclad.language.ErlangElementTypes"
  //elementTypePrefix="TOK_"
  elementTypeClass="se.clau.ironclad.language.ErlangElementType"
  tokenTypeClass="se.clau.ironclad.language.ErlangTokenType"
}

ErlangFile ::= ErlangFileBody | EscriptFileBody | TermsFileBody

private TermsFileBody ::= &<<isTermsSyntaxFile>> LiteralExprWithPeriod *
private ErlangFileBody ::= &<<isErlangSyntaxFile>> Form *
private EscriptFileBody ::= &<<isEscriptSyntaxFile>> Expr ( COMMA Expr ) * PERIOD

// An Erlang module form is an attribute, or a function definition
//private formWithPeriod ::= form PERIOD {recoverWhile=formRecover pin=1}
//private formRecover ::= !(PLUS | MINUS | L_DOUBLE_ANGLE | QUESTION
//    | L_SQUARE | L_CURLY | atom | BINARY_NOT | CHAR_LITERAL
//    | float | INTEGER_LITERAL | NOT | STRING_LITERAL | VAR | HASH_SYMBOL | PERIOD)
private Form ::= FnDef
    | PreprocessorDefine
    | ModuleAttr
    | !<<isEofOrSpace>>

private LiteralExprWithPeriod ::= LiteralExpr PERIOD {pin=1}

//
// General Erlang syntax constructs, shared with different file types
//
// Literal expression contains only static parts, no variables or function calls
LiteralExpr ::= FLOAT_LITERAL | <<parseFloatLiteral>> | INTEGER_LITERAL
    | DOLLAR_CHAR | STRING_LITERAL | CHAR_LITERAL
    | Atom

// Expr same as literal expr but allows function calls and variables
Expr ::= VAR | LiteralExpr
Atom ::= ATOM_NAME | (SINGLE_QUOTE ATOM_NAME SINGLE_QUOTE) {pin(".*") = 1}

//
// Erlang Module constructs
//
ModuleAttr ::= MINUS Atom ( L_PAREN ModuleAttrContents R_PAREN )? PERIOD {pin=2}
private ModuleAttrContents ::= LiteralExpr? ( COMMA LiteralExpr )*

FnDef ::= Atom L_PAREN FnDefArgs R_PAREN R_ARROW FnDefBody PERIOD
private FnDefArgs ::= FnDefArg ( COMMA FnDefArg )*
FnDefArg ::= VAR | Atom // TODO: Proper match expression syntax
private FnDefBody ::= Expr ( COMMA Expr )*

//
// Preprocessor
//
PreprocessorDirectiveEnd ::= R_PAREN PERIOD
private preprocessorMacroIdent ::= VAR | ATOM_NAME // lowercase or uppercase, all good
// Matches a "-define" "(" MACROIDENT ( ARGS "," ... )<optional> "," <tokens> ")."
//external PreprocessorDefine ::= handlePreprocessorDefine PreprocessorDefine_
PreprocessorDefine ::= PP_DEFINE L_PAREN preprocessorMacroIdent
    PreprocessorDefineArgs? COMMA
    PreprocessorMacroBodyToken*
    PreprocessorDirectiveEnd {pin=3}
PreprocessorDefineArgs ::= L_PAREN VAR ( COMMA VAR )* R_PAREN
private PreprocessorDirectiveRecover ::= !PreprocessorDirectiveEnd
// Matches one any token other than ")."
private PreprocessorMacroBodyToken ::= <<macroBodyAnyToken>> {
    pin=1
    recoverWhile=PreprocessorDirectiveRecover
}
